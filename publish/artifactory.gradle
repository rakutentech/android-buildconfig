def repo = project.hasProperty('ARTIFACTORY_REPO') ? project.ARTIFACTORY_REPO : System.getenv('ARTIFACTORY_REPO')
def user = project.hasProperty('ARTIFACTORY_USER') ? project.ARTIFACTORY_USER : System.getenv('ARTIFACTORY_USER')
def password = project.hasProperty('ARTIFACTORY_PASSWORD') ? project.ARTIFACTORY_PASSWORD : System.getenv('ARTIFACTORY_PASSWORD')
def url = project.hasProperty('ARTIFACTORY_URL') ? project.ARTIFACTORY_URL : System.getenv('ARTIFACTORY_URL')

publishing {
  repositories {
    maven {
      it.name repo
      credentials {
        it.username user
        it.password password
      }
      it.url url
    }
  }
}

task ensureArtifactoryCredentials {
  doFirst {
    if (!repo?.trim() || !user?.trim() || !password?.trim() || !url?.trim()) {
      throw new GradleException("You must define environment variables for " +
              "ARTIFACTORY_USER, ARTIFACTORY_PASSWORD, ARTIFACTORY_URL, and ARTIFACTORY_REPO.")
    }
  }
}

publish.dependsOn ensureArtifactoryCredentials
