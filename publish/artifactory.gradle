def repo = project.ext.hasProperty('ARTIFACTORY_REPO') ? project.ext.ARTIFACTORY_REPO : System.getenv('ARTIFACTORY_REPO')
def user = project.ext.hasProperty('ARTIFACTORY_USER') ? project.ext.ARTIFACTORY_USER : System.getenv('ARTIFACTORY_USER')
def password = project.ext.hasProperty('ARTIFACTORY_PASSWORD') ? project.ext.ARTIFACTORY_PASSWORD : System.getenv('ARTIFACTORY_PASSWORD')
def url = project.ext.hasProperty('ARTIFACTORY_URL') ? project.ext.ARTIFACTORY_URL : System.getenv('ARTIFACTORY_URL')

publishing {
  repositories {
    maven {
      it.name repo
      credentials {
        it.username user
        it.password password
      }
      it.url url
    }
  }
}

task ensureArtifactoryCredentials {
  doFirst {
    if (!repo?.trim() || !user?.trim() || !password?.trim() || !url?.trim()) {
      throw new GradleException("You must define environment variables for " +
              "ARTIFACTORY_USER, ARTIFACTORY_PASSWORD, ARTIFACTORY_URL, and ARTIFACTORY_REPO.")
    }
  }
}

publish.dependsOn ensureArtifactoryCredentials
