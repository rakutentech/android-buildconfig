apply plugin: 'com.jfrog.bintray'

def user = project.ext.hasProperty('BINTRAY_USER') ? project.ext.BINTRAY_USER : System.getenv('BINTRAY_USER')
def key = project.ext.hasProperty('BINTRAY_KEY') ? project.ext.BINTRAY_KEY : System.getenv('BINTRAY_KEY')
def repo = project.ext.hasProperty('BINTRAY_REPO') ? project.ext.BINTRAY_REPO : System.getenv('BINTRAY_REPO')
def pkgName = project.ext.hasProperty('BINTRAY_PACKAGE_NAME') ? project.ext.BINTRAY_PACKAGE_NAME : System.getenv('BINTRAY_PACKAGE_NAME')

bintray {
  it.user = user
  it.key = key
  publications = publishing.publications.collect{ it.name }
  pkg {
    it.repo = repo
    name = pkgName
    licenses = ['MIT']
    vcsUrl = project.scmUrl
    version {
      name = project.version
      desc = project.description
      released  = new Date()
      vcsTag = "v$project.version"
    }
  }
}

task ensureBintrayCredentials {
  doFirst {
    if (!user?.trim() || !key?.trim() || !repo?.trim()) {
      throw new GradleException("You must define environment variables for " +
              "BINTRAY_USER, BINTRAY_KEY, and BINTRAY_REPO.")
    }
  }
}

bintrayUpload.dependsOn ensureBintrayCredentials
publish.dependsOn bintrayUpload
