apply plugin: 'org.jetbrains.dokka'

def docsBuildDirectory = "$buildDir/kdocs"

dokkaGfm.configure {

  dokkaSourceSets {
    configureEach {
      outputDirectory.set(file("$docsBuildDirectory"))
      noAndroidSdkLink.set(false)
      noJdkLink.set(false)
      noStdlibLink.set(false)
      noAndroidSdkLink.set(false)
      includeNonPublic.set(false)
      skipEmptyPackages.set(true)
      skipDeprecated.set(false)
      reportUndocumented.set(true)
    }
  }
}

def (major, minor) = project.version.tokenize('.')
def version = "$major.$minor"

def docsDirectory = "$buildDir/publishableDocs"
def docsVersionDirectory = "$docsDirectory/docs/$version"

task copyKDocs(type: Copy) {
  dependsOn dokkaGfm
  from "$docsBuildDirectory"
  into "$docsVersionDirectory/api"
}

task copyUserGuide(type: Copy) {
  into docsVersionDirectory
  into ("") {
    from "USERGUIDE.md"
  }
  rename "USERGUIDE.md", "index.md"
  into ("images") {
    from "images"
  }
}

task generatePublishableDocs {
  dependsOn copyKDocs
  dependsOn copyUserGuide
  doLast {
    def versionFile = file("$docsDirectory/_versions/${version}.md")
    versionFile.parentFile.mkdirs()
    versionFile.text = """
      ---
      version: $version
      ---
    """.stripIndent().trim()
  }
}