apply plugin: 'org.jetbrains.dokka'

def docsBuildDirectory = "$buildDir/kdocs"

dokka {
  outputFormat = 'html'
  outputDirectory = "$docsBuildDirectory"
  configuration {
    includeNonPublic = false
    skipEmptyPackages = true
  }
}

def (major, minor) = project.version.tokenize('.')
def version = "$major.$minor"

def docsDirectory = "$buildDir/publishableDocs"
def docsVersionDirectory = "$docsDirectory/docs/$version"

task copyKDocs(type: Copy) {
  dependsOn dokka
  from "$docsBuildDirectory/$project.name"
  into "$docsVersionDirectory/api"
}

task copyUserGuide(type: Copy) {
  into docsVersionDirectory
  into ("") {
    from "USERGUIDE.md"
  }
  into ("images") {
    from "images"
  }
}

task generatePublishableDocs {
  dependsOn copyKDocs
  dependsOn copyUserGuide
  doLast {
    def versionFile = file("$docsDirectory/_versions/${version}.md")
    versionFile.parentFile.mkdirs()
    versionFile.text = """
      ---
      version: $version
      ---
    """.stripIndent().trim()
  }
}