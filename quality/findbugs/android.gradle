apply plugin: 'findbugs'

import java.nio.file.Paths

configurations {
  findbugsPlugins
}

findbugs {
  toolVersion = "3.0.1" // see https://github.com/findbugsproject/findbugs/releases
  effort = 'max' //  min, default, or max.
  ignoreFailures = false
  excludeFilter = rootProject.file(Paths.get('config', 'quality', 'findbugs', 'findbugs-filter.xml'))
  reportLevel = 'low' // low, medium, high
}

check.dependsOn 'findbugs'
task findbugs(type: FindBugs,
    group: 'verification',
    description: 'findbugs checks for main and test java sourcesets of android projects',
    dependsOn: 'compileReleaseSources' ) {

  project.afterEvaluate {
    def variants = android.hasProperty('libraryVariants') ? android.libraryVariants : android.applicationVariants
    def release = variants.find { it.name == 'release' }
    classpath = files(android.bootClasspath) + release.javaCompileProvider.get().classpath
  }

  classes = files(Paths.get("$buildDir", 'intermediates', 'javac', 'release'))
  pluginClasspath = project.configurations.findbugsPlugins
  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  effort = 'max'
  reportLevel = 'low'

  reports {
    xml.enabled = false
    html.enabled = true
  }
}

dependencies {
  compileOnly 'com.google.code.findbugs:annotations:3.0.1'
  findbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.9.0'
}

android {
  defaultConfig {
    consumerProguardFiles rootProject.file('config/quality/findbugs/proguard-rules')
  }
}
